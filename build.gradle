apply plugin: 'war'

ext {
    wildflyVersion = '8.1.0.Final'
}

repositories {
    jcenter()
}

configurations {
    wildfly
}

dependencies {
    // wildfly-dist-8.1.0.Final.zip
    wildfly "org.wildfly:wildfly-dist:${wildflyVersion}@zip"
    compileOnly "org.projectlombok:lombok:1.16.10"
    compile group: 'org.jboss.resteasy', name: 'resteasy-jaxrs', version: '3.0.19.Final'
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.jaxrs/jackson-jaxrs-json-provider
    compile group: 'com.fasterxml.jackson.jaxrs', name: 'jackson-jaxrs-json-provider', version: '2.8.3'

    // testing tools
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'org.hamcrest:hamcrest-integration:1.3'
    // main BOM for Arquillian
    testCompile 'org.jboss.arquillian:arquillian-bom:1.1.6.Final-wildfly-1'
    testCompile 'org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-impl-maven:3.0.0-alpha-1'

            // JUnit container
    testCompile 'org.jboss.arquillian.junit:arquillian-junit-container:1.1.11.Final'
    // REST client
    testCompile 'org.jboss.arquillian.extension:arquillian-rest-client-impl-jersey:1.0.0.Alpha4'
    // Chameleon (or any other specific container, e.g. JBoss, Wildfly etc.)
    testCompile 'io.rest-assured:rest-assured:3.0.1'
    testRuntime group: 'org.wildfly', name: 'wildfly-arquillian-container-embedded', version: '8.2.0.Final'
    testRuntime 'org.jboss.spec:jboss-javaee-7.0:1.0.3.Final'
}

task wildfly(type: Copy) {
    dependsOn configurations.wildfly
    configurations.wildfly.asFileTree.each {
        from(zipTree(it))
    }
    into project.buildDir
}

test {
    dependsOn wildfly
    systemProperty 'jboss.home', file("${project.buildDir}/wildfly-${wildflyVersion}").toString()
    systemProperty 'java.util.logging.manager', 'org.jboss.logmanager.LogManager'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}
